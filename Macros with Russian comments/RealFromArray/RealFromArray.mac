! Создание вещественной константы из массива данных.
!
! Состав необходимых макросов:
! - Vectorization.mac
! - RealFromArrayRCall.mac
!
! имя:
!    *use, RealFromArray.mac, ARG1, ARG2, ARG3
!{ ! О П И С А Н И Е
! Аргументы макроса могут принимать следующие значения.
!    ARG1:
!    |
!    >>- 'INIT'     - Инициальзация. Создание переменных, которым присваиваются исходные
!    |                данные пользователя.
!    |                  ARG2 - Начальное значение номера "NSET" вещественной постоянной:
!    |                  |
!    |                  >>- 'NEXT' - Для команды Ansys APDL "R, NSET, R1, R2, R3, R4, R5, R6"
!    |                  |            начальное значение номера "NSET" вещественной постоянной 
!    |                  |            назначается следующим за наибольшим найденным в базе
!    |                  |            данных ( *GET,NSET,RCON,0,NUM,MAX $ NSET = NSET+1 ).
!    |                  |            Дальнейшее приращение номера вещественной постоянной 
!    |                  |            производится автоматически с шагом в единицу.
!    |                  |
!    |                  >>- Num    - Начальное значение номера "NSET" вещественной постоянной,
!    |                               которое задаётся Пользователем (NSET = Num). 
!    |                               Дальнейшее приращение номера вещественной постоянной 
!    |                               производится автоматически с шагом в единицу.
!    |
!    |                  ARG3 - Количество выбранных позиций массива для передачи этих
!    |                  |      позиций в команду создания вещественной постоянной:
!    |                  >>- 'ALL'  - Выбираются все позиции массива.
!    |                  |               Команда
!    |                  |                  *use, RealFromArray.mac, 'INIT', 'NEXT', 'ALL'
!    |                  |               Или
!    |                  |                  *use, RealFromArray.mac, 'INIT', Num, 'ALL'
!    |                  |
!    |                  >>- Cnt    - Количество позиций массива, выбранных пользователем.
!    |                                  Команда
!    |                                     *use, RealFromArray.mac, 'INIT', 'NEXT', 'ALL'
!    |                                  Или
!    |                                     *use, RealFromArray.mac, 'INIT', Num, Cnt
!    |                               В результате выполнения одной из двух команд,
!    |                               приведённых выше, создаётся массив rfa_SelPos(1),
!    |                               которому присваиваются номера выбранных позиций.
!    |                         Для Метода "ПЛОСКОСТЬ" позицией называется номер его 
!    |                         плоскости(Plane), а для метода "СТРОКА" позиция - номер 
!    |                         строки. Методы работы макроса описаны ниже.
!    |                                
!    |                После выполнения одной из вышеизложенных команд необходимо 
!    |                переменной v_ArrayName(1) присвоить имя массива, предназначенного
!    |                для передачи его значений в команду создания вещественных постоянных.
!    |
!    >>- 'PREPARE'  - Создание параметров, необходимых для работы макроса.
!    |                  ARG2 - Метод работы макроса:
!    |                  |
!    |                  >>- 'PLANE' - Метод "ПЛОСКОСТЬ". При данном методе из массива 
!    |                  |             размером MxNxP его часть MxN, находящаяся в 
!    |                  |             конкретной плоскости j переводится в одну вещественную
!    |                  |             постоянную.
!    |                  |               Команда
!    |                  |                  *use, RealFromArray.mac, 'PREPARE', 'PLANE'
!    |                  |             Пример.
!    |                  |             Массив А, размером (3,2,1), где 
!    |                  |                       вектор A(,1,1) -  D, перемещение, 
!    |                  |                       вектор A(,2,1) -  F, сила.
!    |                  |             Массив A в команду создания вещественных постоянных
!    |                  |             будет заноситься следующим образом.
!    |                  |             R, NSET, A(1,1), A(1,2), A(2,1), A(2,2), A(3,1), A(3,2)
!    |                  |
!    |                  >>- 'ROW'   - Метод "СТРОКА". При данном методе из массива
!    |                                размером MxN его строка под номером i переводится
!    |                                в одну вещественную постоянную.
!    |                                  Команда
!    |                                     *use, RealFromArray.mac, 'PREPARE', 'ROW'
!    |                                Пример.
!    |                                Массив B, размером (2,3,1), где
!    |                                          вектор B(,1,1) -  Dout, внешний диаметр,
!    |                                          вектор B(,2,1) -  t,    толщина,
!    |                                          вектор B(,3,1) -  R,    радиус отвода.
!    |                                Массив B в команду создания вещественных постоянных
!    |                                будет заноситься следующим образом.
!    |                                R, NSET, A(1,1), A(1,2), A(1,3)
!    |                                R, NSET+1, A(2,1), A(2,2), A(2,3)
!    |
!    >>- 'PROCESS'  - Процесс перевода значений из массива в вещественные постоянные 
!    |                по заданному методу.
!    |                   Команда
!    |                      *use, RealFromArray.mac, 'PROCESS'
!    |                В результате выполнения макроса в Вещественной Постоянной с 
!    |                заданными программой номерами сохраняются значения из 
!    |                указанного массива.
!    |
!    >>- 'ERASE'    - Очистка памяти от рабочих масивов и скаляров, а так же от массивов результатов.
!                        Команда
!                           *use, RealFromArray.mac, 'ERASE'
!
!} ! (ВСЁ, что ВЫШЕ) О П И С А Н И Е
! Автор: Макаревич Дмитрий
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
*IF , ARG1, EQ, 'INIT', THEN
	!{ ! Инициальзация
	*DEL, rfa_ArrayName,, NOPR
	*DIM, rfa_ArrayName, STRING, 32         ! имя массива
	*SET, rfa_RCNum, ARG2                   ! назначается первый порядковый номер,                
	                                           ! заданный пользователем или 'NEXT'
	*SET, rfa_SelCnt, ARG3                  ! количество выбранных позиций
	*GET, rfa_SelInitType, PARM, rfa_SelCnt, TYPE ! тип локальной переменной 
	*IF, rfa_SelInitType, EQ, 0, THEN ! скаляр
		*DEL, rfa_SelPos,, NOPR
		*DIM, rfa_SelPos, ARRAY, rfa_SelCnt ! массив с номерами выбранных позиций
	*ENDIF
	
	! определение первого порядкового номера вещественной постоянной
	*GET, rfa_RCInitType, PARM, rfa_RCNum, TYPE ! тип локальной переменной     
	*IF, rfa_RCInitType, EQ, 3, THEN         ! строка
		*IF , rfa_RCNum, EQ, 'NEXT', THEN    ! пользователь назначает первый
			! порядковый номер вещественной постоянной следующий за последним
			! найденным в базе данных
			*GET,rfa_RCNum,RCON,0,NUM,MAX
			*IF,rfa_RCNum,LT,10**(-6),THEN   ! случай, когда в системе 
				! не определено ни одной вещественной постоянной			
				rfa_RCNum = 1
			*ELSE                            ! в противном случае к максимальному
				! найденному номеру прибавляется единица 
				rfa_RCNum = rfa_RCNum + 1
			*ENDIF					
		*ENDIF			
	*ENDIF
	!}
*ELSEIF , ARG1, EQ, 'PREPARE'
	!{ ! Подготовка параметров для работы макроса
	*SET, rfa_Method, ARG2 ! метод работы макроса
	*IF , rfa_Method, EQ, 'PLANE', THEN ! подготовка к созданию вещественных постоянных 
                                        ! по методу "ПЛОСКОСТЬ" 	
		*IF , rfa_SelInitType, EQ, 3, THEN ! строка  
			*GET, rfa_SelCnt, PARM, %rfa_ArrayName(1)%, DIM, 3 ! количество плоскостей
			                                              ! (Plane) в исходном массиве
		*ENDIF	
		*use, Vectorization.mac, 'INIT'    ! Инициализация макроса для векторизации 
		                                                        ! исходного массива
		v_ArrayName(1) = rfa_ArrayName(1)  ! присвоение переменной v_ArrayName(1)
		                                               ! имени исходного массива
		*use, Vectorization.mac, 'PREPARE' ! подготовка макроса к работе
		*GET, rfa_v_Result_d1, PARM, v_Result(1), DIM, 1 ! размерность массива с
		                                ! результатами макроса Vectorization.mac
		rfa_R_d1 = ( NINT(rfa_v_Result_d1/6) + 1 )*6 ! размер массива, предназначенного
		                                         ! для создания вещественных постоянных
												 
	*ELSEIF, rfa_Method, EQ, 'ROW'      ! подготовка к созданию вещественных постоянных
                                        ! по методу "СТРОКА"
		*IF , rfa_SelInitType, EQ, 3, THEN ! скаляр
			*GET, rfa_SelCnt, PARM, %rfa_ArrayName(1)%, DIM, 1 ! количество строк
			                                                 ! в исходном массиве
		*ENDIF
		*GET, rfa_Array_d2, PARM, %rfa_ArrayName(1)%, DIM, 2   ! количество столбцов
		rfa_R_d1 = ( NINT(rfa_Array_d2/6) + 1 )*6 ! размер массива, предназначенного
				                              ! для создания вещественных постоянных
		
	*ENDIF
	*IF , rfa_SelInitType, EQ, 3, THEN
		*DEL, rfa_SelPos,, NOPR
		*DIM, rfa_SelPos, ARRAY, rfa_SelCnt ! массив позиций
		*VFILL, rfa_SelPos(1), RAMP, 1, 1	! назначение позиций по порядку с 
							          ! первой до последней с шагом в единицу	
	*ENDIF
	*DEL, rfa_R,, NOPR
	*DIM, rfa_R, ARRAY, rfa_R_d1            ! массив для Real Constants
		                                    ! (вещественных постоянных)
	rfa_CyclesCnt = rfa_R_d1/6  ! количество шагов цикла кратно шести, т.к. за один раз
	                        ! в команды R или RMORE можно занести только шесть значений										
	!}
*ELSEIF , ARG1, EQ, 'PROCESS'
	!{ ! Часть функции, связання непосредственно с добавлением вещественных постоянных
	*IF , rfa_Method, EQ, 'PLANE', THEN ! создания вещественных постоянных
                                        ! по методу "ПЛОСКОСТЬ"
		*DO, rfa_j, 1, rfa_SelCnt, 1 ! обход в цикле по назначенным позициям плоскостей
		                                                              ! (Plane) массива
			*use, Vectorization.mac, 'PROCESS', 'ROW', rfa_SelPos(rfa_j) ! векторизация
			                                            ! массива на заданной плоскости
			*VFUN, rfa_R(1), COPY, v_Result(1) ! копирование векторизованного массива
			! в связующий массив (интерфейс), значения из которого заносятся в команду
			                            ! создания вещественных постоянных (R и RMORE)
			*use, RealFromArrayRCall.mac
		*ENDDO
	*ELSEIF, rfa_Method, EQ, 'ROW'      ! создания вещественных постоянных
	                                    ! по методу "СТРОКА"
		*MFUN, rfa_ArrayT, TRAN, %rfa_ArrayName(1)%
		*DO, rfa_j, 1, rfa_SelCnt, 1
			*VFUN, rfa_R(1), COPY, rfa_ArrayT(1,rfa_SelPos(rfa_j))
			*use, RealFromArrayRCall.mac			
		*ENDDO

	*ENDIF	
	!}
*ELSEIF, ARG1, EQ, 'ERASE'
	!{ ! Очистка Памяти от Массивов и Скаляров, связанных с работой макроса
	*DEL, rfa_ArrayName,, NOPR
	*DEL, rfa_RCInitType
	*DEL, rfa_SelInitType
	*DEL, rfa_RCNum
	*DEL, rfa_SelCnt
	*DEL, rfa_SelPos,, NOPR
	
	*DEL, rfa_Method
	*DEL, rfa_v_Result_d1
	*DEL, rfa_R_d1
	*DEL, rfa_R,, NOPR
	*DEL, rfa_CyclesCnt
	
	*DEL, rfa_ArrayT,, NOPR
	*DEL, rfa_Array_d2
	*DEL, rfa_i
	*DEL, rfa_j
	
	*use, Vectorization.mac, 'ERASE'	
	!}
*ENDIF





