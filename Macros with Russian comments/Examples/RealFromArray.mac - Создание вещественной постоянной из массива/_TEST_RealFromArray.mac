! пример использования макроса *use, RealFromArray.mac, ARG1, ARG2, ARG3
! имя данного тестового макроса:
!    *use, _TEST_RealFromArray.mac
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
FINISH                               ! 1. Очистка базы данных на случай повторного 
/CLEAR,NOSTART                       !    использования данного примера.
/PREP7                               ! 2. Вход в меню препроцессора.
! Вещественные Постоянные для трубы
! из строк массива
*DIM, PipeProperty, ARRAY, 3, 2      ! 3. Массив с геометрическими характеристиками трубы,
                                     !    которые необходимо внести в качестве
									 !    Вещественных Постоянных (командой R)
PipeProperty(1,1) = 530,   720,   1020 !  D, [mm] - диаметр
PipeProperty(1,2) = 14.2,  19.3,  27.3 !  t, [mm] - толщина
                                     !
*use, RealFromArray.mac,'INIT', 'NEXT', 'ALL' ! 4. Инициализация макроса, с помощью которого
                                     !    будет произведено внесение в базу данных
									 !    вещественных постоянных из массива PipeProperty.
									 !    ARG2 = 'NEXT'. Макрос самостоятельно определяет
									 !    последний наибольший номер вещественной постоянной
									 !    i. Строка массива j помещается в вещественную
									 !    постоянную с номером k = i+1+j.
									 !    ARG3 = 'ALL'. Все строки массива добавляются в
									 !    вещественные постоянные.
rfa_ArrayName(1) = 'PipeProperty'    ! 5. Переменной rfa_ArrayName(1) присваивается строка
                                     !    с именем массива, значения которого предназначены
									 !    для создания вещественных постоянных.
*use, RealFromArray.mac,'PREPARE','ROW' ! 6. Подготовка переменных и массивов макроса
                                     !    к процедуре создания вещественных постоянных.
*use, RealFromArray.mac,'PROCESS'    ! 7. Выполнение процедуры создания вещественных 
                                     !    постоянных из строк массива.	 
! Вещественные Постоянные для пружин
! из плоскостей (Plane) массива
*DIM, CombineProperty, ARRAY, 7, 2, 2 ! 8. Массив, из двух плоскостей которого будут созданы
                                      !    две вещественные постоянные для пружин.
									  ! 9. Свойства первой пружины.
CombineProperty(1,1,1) =   -10,  -5,  -2, 0, 1,  5,  6  ! Displacement, D - перемещения
CombineProperty(1,2,1) =   -100, -100, 0, 0, 50, 35, 20 ! Force, F        - сила
                                      ! 10. Свойства второй пружины.
CombineProperty(1,1,2) =   -15,  -10,  0, 10,  15 ! D
CombineProperty(1,2,2) =   -300, -300, 0, 100, 50 ! F	
                                      ! 9. В Вещественные постоянные значения будут вноситься
									  !    по следующей схеме:
									  !    R, k, CombineProperty(1,1,1), CombineProperty(1,2,1), ...
									  !    RMORE, CombineProperty(7,1,1), CombineProperty(7,2,1)
									  !    R, k+1, CombineProperty(1,1,2), CombineProperty(1,2,2), ...
rfa_ArrayName(1) = 'CombineProperty'  ! 10. Для перевода в вещественные постоянные массива 
                                      !		CombineProperty указываем имя данного массива 
									  !		в строку переменной rfa_ArrayName(1).
*use, RealFromArray.mac,'PREPARE','PLANE' ! 11. Подготовка переменных и массивов макроса
									  !		к созданию вещественных постоянных из полоскостей
									  !		массива.									  
*use, RealFromArray.mac,'PROCESS'     ! 12. Выполнение процедуры создания вещественных
									  !		постоянных.
*use, RealFromArray.mac,'ERASE'       ! 13. Очистка памяти от рабочих параметров и макросов.
! Проверка работы макроса
!    Характеристики трубы
*DO, i, 1, 3, 1                       ! 14. Цикл по трём вещественным постоянным
	*GET,R_%i%_Tube_D,RCON,i,CONST,1  ! 15. Диаметр 
	*GET,R_%i%_Tube_t,RCON,i,CONST,2  ! 16. Толщина 	
*ENDDO
!    Характеристики пружины
*DO, i, 4, 5, 1                       ! 17. Цикл по двум вещественным постоянным
	*DO, j, 1, 7, 1                   ! 18. Цикл по величинам вещественных постоянных
		*GET,R_%i%_Combine_D_%j%,RCON,i,CONST,2*j-1 ! Величины, касающиеся перемещений с 1 по 13
		*GET,R_%i%_Combine_F_%j%,RCON,i,CONST,2*j	! Величины сил с 2 по 14
	*ENDDO
*ENDDO













