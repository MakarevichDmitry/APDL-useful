! Для исходного массива размером MxNxP векторизация его части из заданной 
! плоскости (Plane) размерностью MxN в массив M*Nx1
!
! имя:
!    *use, Vectorization.mac, ARG1, ARG2, ARG3
!{ ! О П И С А Н И Е
! Аргументы макроса могут принимать следующие значения.
!    ARG1:
!    |
!    >>- 'INIT'     - Инициальзация. Создание переменной, которой присваивается имя
!    |                массива предназначенного для векторизации.
!    |                   Команда
!    |                      *use, Vectorization.mac, 'INIT'
!    |                После выполнения команды необходимо переменной v_ArrayName(1)
!    |                присвоить имя массива предназначенного для векторизации. 
!    |
!    >>- 'PREPARE'  - Создание параметров, необходимых для работы макроса.                                                            
!    |                   Команда
!    |                      *use, Vectorization.mac, 'PREPARE'
!    |                Команда выполняется после присвоения переменной v_ArrayName(1)
!    |                имени массива для векторизации. А так же если после вызова
!    |                команды *use, Vectorization.mac, 'PROCESS', ARG2, ARG3 
!    |                Пользователю понадобилось снова использовать команду
!    |                *use, Vectorization.mac, 'PROCESS', ARG2, ARG3 для другого
!    |                массива, предназначенного для векторизации.
!    |
!    >>- 'PROCESS'  - Векторизация массива. Результаты сохраняются в массив v_Result(1)
!    |                  ARG2:
!    |                  |
!    |                  >>- 'ROW'    - метод векторизации "СТРОКА". Ячейка (i,j) из
!    |                  |              исходного массива, где количество строк M, а 
!    |                  |              количество столбцов N, в векторе с результатом
!    |                  |              будет располагаться в ячейке k = j+N*(i-1).                                   
!    |                  |                 Команда
!    |                  |                    *use, Vectorization.mac, 'PROCESS', 'ROW', ARG3
!    |                  |
!    |                  >>- 'COLUMN' - метод векторизации "СТОЛБЕЦ". Ячейка (i,j) из
!    |                                 исходного массива в векторе с результатом будет
!    |                                 располагаться в ячейке k = i+N*(j-1).
!    |                                    Команда
!    |                                       *use, Vectorization.mac, 'PROCESS', 'COLUMN', ARG3
!    |
!    |                  ARG3 - номер плоскости (Plane) массива, который необходимо векторизовать.
!    |
!    >>- 'ERASE'    - Очистка памяти от рабочих масивов и скаляров, а так же от массива результата.
!                       Команда
!                          *use, Vectorization.mac, 'ERASE'
!
!} ! (ВСЁ, что ВЫШЕ) О П И С А Н И Е
! Автор: Макаревич Дмитрий
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
*IF , ARG1, EQ, 'INIT', THEN
	!{ ! Инициальзация
	*DIM, v_ArrayName, STRING, 32                    ! имя массива для векторизации
	!}
*ELSEIF , ARG1, EQ, 'PREPARE'
	!{ ! Подготовка параметров для работы макроса
	*DEL, v_Result,, NOPR            ! Для случая повторного использования;
	*DEL, v_PositionInResult,, NOPR	 ! при другом значении строки в v_ArrayName(1)
	*GET, v_Array_d1, PARM, %v_ArrayName(1)%, DIM, 1 ! кол-во строк (M)
	*GET, v_Array_d2, PARM, %v_ArrayName(1)%, DIM, 2 ! кол-во столбцов (N)
	
	*DIM, v_Result, ARRAY, v_Array_d1*v_Array_d2     ! массив (вектор) с
	! результатом векторизации с общим числом ячеек равным числу 
	! ячеек в исходном массиве для векторизации	
	*DIM, v_PositionInResult, ARRAY, v_Array_d1      ! Для метода векторизации 
	! "строка". Вектор, в котором создаются значения индексов, по которым копируется
	! содержимое исходного массива в массив результатов
	!}
*ELSEIF , ARG1, EQ, 'PROCESS'
	!{ ! Часть функции, связання непосредственно с векторизацией
	*IF , ARG2, EQ, 'ROW', THEN ! метод векторизации "строка"		
		*DO, v_i, 1, v_Array_d2, 1 ! цикл по всем столбцам исходного массива
			*VFILL, v_PositionInResult(1), RAMP, v_i, v_Array_d2
			*VOPER, v_Result(1), %v_ArrayName(1)%(1,v_i,ARG3), SCAT, v_PositionInResult(1)
		*ENDDO
	*ELSEIF, ARG2, EQ, 'COLUMN' ! метод векторизации "столбец" 
		*DO, v_i, 1, v_Array_d2, 1 ! цикл по всем столбцам исходного массива
			*VFUN, v_Result( (v_i-1)*v_Array_d1 + 1 ), COPY, %v_ArrayName(1)%(1,v_i,ARG3)	
		*ENDDO
	*ENDIF	
	!}
*ELSEIF, ARG1, EQ, 'ERASE'
	!{ ! Очистка Памяти от Массивов и Скаляров, связанных с работой макроса
	*DEL, v_ArrayName,, NOPR
	
	*DEL, v_Array_d1
	*DEL, v_Array_d2
	*DEL, v_Result,, NOPR
	*DEL, v_PositionInResult,, NOPR
	*DEL, v_i
	!}
*ENDIF

